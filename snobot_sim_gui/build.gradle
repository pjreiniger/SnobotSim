
import org.gradle.internal.os.OperatingSystem


evaluationDependsOn(':snobot_sim_utilities')
evaluationDependsOn(':sim_adx_family')
evaluationDependsOn(':sim_extension_navx')
evaluationDependsOn(':temp_hal_interface')

ext
{
    baseId = "snobot_sim_gui"
}

apply from: "${rootDir}/common/base_java_script.gradle"

configurations {
    native3rdPartyDeps
    compile.extendsFrom(native3rdPartyDeps)
}

apply from: "$rootDir/captureWpiLibraries.gradle"
dependencies {
    
    // WPILib
    compile 'edu.wpi.first.wpilibj:wpilibj-java:' + allwpilibVersion()
    compile 'edu.wpi.first.wpiutil:wpiutil-java:' + getWpiUtilVersion()
    compile 'edu.wpi.first.cscore:cscore-java:' + getCsCoreVersion()
    runtime 'edu.wpi.first.cscore:cscore-jni:' + getCsCoreVersion() + ':all'
    compile 'edu.wpi.first.ntcore:ntcore-java:' + getNtCoreVersion()
    runtime 'edu.wpi.first.ntcore:ntcore-jni:' + getNtCoreVersion() + ':all'
    compile 'org.opencv:opencv-java:' + getWpilibOpencvVersion()
    runtime 'org.opencv:opencv-jni:' + getWpilibOpencvVersion() + ':all'
    
    // 3rd Party
    native3rdPartyDeps  'net.java.jinput:jinput:2.0.7'
    compile 'jfree:jcommon:1.0.16'
    compile 'jfree:jfreechart:1.0.13'
    compile 'org.apache.logging.log4j:log4j-api:2.11.0'
    compile 'org.apache.logging.log4j:log4j-core:2.11.0'
    compile 'org.yaml:snakeyaml:1.18'
    compile 'com.miglayout:miglayout-swing:4.2'
    //compile 'org.python:jython:2.7.1b3'
    
    // Internal
    compile project(":snobot_sim_utilities")
    
    def os_name = org.gradle.internal.os.OperatingSystem.current().getFamilyName();
    if(build_simulator_cpp)
    {
        compile project(":snobot_sim_jni")
        runtime     files("${rootDir}/snobot_sim_jni/build/libs/snobot_sim_jni-uber_native-" + os_name  +".jar")
        testRuntime files("${rootDir}/snobot_sim_jni/build/libs/snobot_sim_jni-uber_native-" + os_name  +".jar")
    }
    
    if(build_simulator_java)
    {
        compile project(":snobot_sim_java")
        runtime captureWpiLibraries.outputs.files
        runtime project(':sim_extension_navx').packageNativenavx_simulator_jniFilesInJar.outputs.files
        runtime project(':sim_adx_family').packageNativeadx_family_jniFilesInJar.outputs.files
    }
    
    // Test
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testRuntime 'org.junit.platform:junit-platform-launcher:1.2.0'
    runtime 'com.snobot.simulator:ctre_sim_override:' + getCtreSimVersion() + ':native-all'
    testRuntime jar.outputs.files // Necessary for having resources in jar 
    
    runtime project(':temp_hal_interface').packageNativetemp_hal_interface_jniFilesInJar.outputs.files
}

apply from: "$rootDir/common/prepare_ctre_library.gradle"
compileJava.dependsOn prepareCtreLibrary
tasks.withType(Test) {
    systemProperty "java.library.path", getCtreNativeExtractedLocation()
}

eclipse.classpath.file.whenMerged { classpath ->
    classpath.entries.each {
        if(it.path.contains("ctre_sim_override")) {
            it.setNativeLibraryLocation(getCtreNativeExtractedLocation())
        }
    }
}


task unzipNativeLibraries(type: Copy) {

    configurations.native3rdPartyDeps.each {
        from zipTree(it)
        into "build/native_libs"
        include "**/*.dll"
        include "**/*.lib"
        include "**/*.pdb"
        include "**/*.so*"
        include "**/*.a"
        include "**/*.dylib*"
    }
    
    includeEmptyDirs = false
        
}

eclipse.classpath.file {
    withXml {
        provider ->
        provider.asNode().findAll { it.@path.contains("jinput") && !it.@path.contains("natives") }.each {
                def container = it
                container.appendNode('attributes').appendNode('attribute', [name: 'org.eclipse.jdt.launching.CLASSPATH_ATTR_LIBRARY_PATH_ENTRY', value:"snobot_sim_gui/build/native_libs"])
        }
    }
}

build.dependsOn unzipNativeLibraries

if(build_simulator_cpp)
{
    compileJava.dependsOn(":snobot_sim_jni:build")
}
if(build_simulator_java)
{
    compileJava.dependsOn(":snobot_sim_java:build")
}

jar {
   baseName = "snobotSim"

   from("${rootDir}/snobot_sim_gui/src/main/java") {
      include  '**/*.png'
      include  '**/*.properties'
      include  '**/*.txt'
   }
}

compileJava {
    apply from: "../create_version_file.gradle"
    createJavaVersion("src/main/java/com/snobot/simulator", "SnobotSimGuiVersion", "com.snobot.simulator", getVersionName())
}
    
clean {
    delete "src/main/java/com/snobot/simulator/SnobotSimGuiVersion.java"
}


findbugs {
    ignoreFailures = true
}
