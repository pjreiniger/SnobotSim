
evaluationDependsOn(':snobot_sim_utilities')
evaluationDependsOn(':sim_adx_family')
evaluationDependsOn(':sim_extension_navx')

ext
{
    baseId = "snobot_sim_gui"
}

apply from: "${rootDir}/common/base_java_script.gradle"

configurations {
    native3rdPartyDeps
    compile.extendsFrom(native3rdPartyDeps)
}

configurations.maybeCreate("wpilibNativeDeps")
dependencies {

    // WPILib
    compile 'edu.wpi.first.wpilibj:wpilibj-java:' + allwpilibVersion()
    compile 'edu.wpi.first.wpiutil:wpiutil-java:' + getWpiUtilVersion()
    compile 'edu.wpi.first.cscore:cscore-java:' + getCsCoreVersion()
    runtime 'edu.wpi.first.cscore:cscore-jni:' + getCsCoreVersion() + ':all'
    compile 'edu.wpi.first.ntcore:ntcore-java:' + getNtCoreVersion()
    runtime 'edu.wpi.first.ntcore:ntcore-jni:' + getNtCoreVersion() + ':all'
    runtime 'edu.wpi.first.hal:hal-jni:' + allwpilibVersion() + ':all'
    compile 'org.opencv:opencv-java:' + getWpilibOpencvVersion()
    runtime 'org.opencv:opencv-jni:' + getWpilibOpencvVersion() + ':all'

    // 3rd Party
    native3rdPartyDeps  'net.java.jinput:jinput:2.0.9'
    compile 'jfree:jcommon:1.0.16'
    compile 'jfree:jfreechart:1.0.13'
    compile 'org.apache.logging.log4j:log4j-api:2.12.1'
    compile 'org.apache.logging.log4j:log4j-core:2.12.1'
    compile 'org.yaml:snakeyaml:1.25'
    compile 'com.miglayout:miglayout-swing:5.2'
    //compile 'org.python:jython:2.7.1b3'

    // Internal
    compile project(":snobot_sim_utilities")
    compile project(":snobot_sim_joysticks")

    if(build_simulator_cpp)
    {
        compile project(":snobot_sim_jni")
        wpilibNativeDeps project(':snobot_sim_jni').packageNativeFiles.outputs.files
    }

    if(build_simulator_java)
    {
        compile project(":snobot_sim_java")
        wpilibNativeDeps project(':sim_extension_navx').packageNativeFiles.outputs.files
        wpilibNativeDeps project(':sim_adx_family').packageNativeFiles.outputs.files
    }

    // Test
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testRuntime 'org.junit.platform:junit-platform-launcher:1.5.1'
    runtime 'com.snobot.simulator:ctre_sim_override:' + getCtreSimVersion() + ':all'
    runtime 'com.snobot.simulator:rev_simulator:' + getRevRoboticsSimVersion() + ':all'
}

apply from: "${rootDir}/common/extract_native_libraries.gradle"
test.dependsOn extract_wpilib


task unzipNativeLibraries(type: Copy) {

    configurations.native3rdPartyDeps.each {
        from zipTree(it)
        into "build/native_libs"
        include "**/*.dll"
        include "**/*.lib"
        include "**/*.pdb"
        include "**/*.so*"
        include "**/*.a"
        include "**/*.dylib*"
    }

    includeEmptyDirs = false

}

eclipse.classpath.file {
    whenMerged { classpath ->
        classpath.entries.each {
            if(it.path.contains("jinput") && !it.path.contains("natives")) {
                it.setNativeLibraryLocation("snobot_sim_gui/build/native_libs")
            }
        }
    }
}

build.dependsOn unzipNativeLibraries

if(build_simulator_cpp)
{
    compileJava.dependsOn(":snobot_sim_jni:build")
}
if(build_simulator_java)
{
    compileJava.dependsOn(":snobot_sim_java:build")
}

sourceSets.main.java.srcDir "${buildDir}/generated/java/"
compileJava {
    apply from: "${rootDir}/common/create_version_file.gradle"
    createJavaVersion("com/snobot/simulator", "SnobotSimGuiVersion", "com.snobot.simulator", getVersionName())
}

clean {
    delete "src/main/java/com/snobot/simulator/SnobotSimGuiVersion.java"
}


spotbugs {
    ignoreFailures = true
}
