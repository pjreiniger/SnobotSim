
evaluationDependsOn(':snobot_sim_utilities')

ext
{
    baseId = "snobot_sim_joysticks"
}

apply from: "${rootDir}/common/base_java_script.gradle"

configurations {
    native3rdPartyDeps
    compile.extendsFrom(native3rdPartyDeps)
}

configurations.maybeCreate("wpilibNativeDeps")
dependencies {
    // WPILib
    compile 'edu.wpi.first.ntcore:ntcore-java:' + getNtCoreVersion()
    compile 'edu.wpi.first.wpilibj:wpilibj-java:' + allwpilibVersion()
    compile 'edu.wpi.first.wpiutil:wpiutil-java:' + getWpiUtilVersion()
    compile 'edu.wpi.first.hal:hal-java:' + allwpilibVersion()

    // 3rd Party
    native3rdPartyDeps 'net.java.jinput:jinput:2.0.9'

    // Internal
    compile project(":snobot_sim_utilities")

    // Test
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testRuntime 'org.junit.platform:junit-platform-launcher:1.5.1'
    testRuntime 'edu.wpi.first.ntcore:ntcore-jni:' + getNtCoreVersion() + ':all'
}

apply from: "${rootDir}/common/extract_native_libraries.gradle"
test.dependsOn extract_wpilib


task unzipNativeLibraries(type: Copy) {

    configurations.native3rdPartyDeps.each {
        from zipTree(it)
        into "build/native_libs"
        include "**/*.dll"
        include "**/*.lib"
        include "**/*.pdb"
        include "**/*.so*"
        include "**/*.a"
        include "**/*.dylib*"
    }

    includeEmptyDirs = false

}

eclipse.classpath.file {
    withXml {
        provider ->
        provider.asNode().findAll { it.@path.contains("jinput") && !it.@path.contains("natives") }.each {
                def container = it
                container.appendNode('attributes').appendNode('attribute', [name: 'org.eclipse.jdt.launching.CLASSPATH_ATTR_LIBRARY_PATH_ENTRY', value:"snobot_sim_gui/build/native_libs"])
        }
    }
}

build.dependsOn unzipNativeLibraries

spotbugs {
    ignoreFailures = true
}
