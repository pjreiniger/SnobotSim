import edu.wpi.first.nativeutils.NativeUtils
import org.gradle.api.file.FileCollection
import org.gradle.internal.os.OperatingSystem


repositories {
    mavenCentral()
    maven {
        url getJaci3rdPartyMirror()
    }
}

apply plugin: 'cpp'
apply plugin: 'visual-studio'
apply plugin: 'edu.wpi.first.NativeUtils'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

apply from: '../config.gradle'


apply from: "../create_version_file.gradle"
createJavaVersion("java/snobot_sim_java/com/snobot/simulator/wrapper_accessors/java", "JaveSimulatorVersion", "com.snobot.simulator.wrapper_accessors.java", getVersionName())

sourceSets {
    main {
        java {
            srcDirs = ["java/snobot_sim_java"]
        }
    }
    test {
        java {
            srcDirs = ["java/unit_tests"]
        }
    }
}
test {
    ignoreFailures = OperatingSystem.current().isLinux()
}

task packageNativeFilesInJar(type: Jar) {
    
    dependsOn jar
    
    destinationDir = project.buildDir
    classifier = "native-" + org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    project.model {
        binaries {
            withType(SharedLibraryBinarySpec) { binary ->
                if (binary.component.name == 'snobotSimJavaJniShared') {
                    from(binary.sharedLibraryFile) {
                        into NativeUtils.getPlatformPath(binary)
                    }
                }
            }
        }
    }
}

apply from: "$rootDir/captureWpiLibraries.gradle"

task combineNativeLibrariesIntoOne(type: Jar) {

    dependsOn packageNativeFilesInJar
    dependsOn captureWpiLibraries

    def os_name = org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    classifier = "uber_native-" + os_name;
    
    packageNativeFilesInJar.outputs.files.each { 
        from(zipTree(it))
    }
    
    captureWpiLibraries.outputs.files.each {
        from(zipTree(it))
    }
    
    project(':ctre_override').packageNativeCtreFilesInJar.outputs.files.each {
        from(zipTree(it))
    }
    
    project(':sim_extension_navx').packageNativeNavxFilesInJar.outputs.files.each {
        from(zipTree(it))
    }
}
build.dependsOn packageNativeFilesInJar

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}
    
dependencies {
    compile project(":snobot_sim_utilities")
    compile 'edu.wpi.first.ntcore:ntcore-java:' + getNtCoreVersion()
    compile 'edu.wpi.first.cscore:cscore-java:' + getCsCoreVersion()
    compile group: 'log4j', name: 'log4j', version: '1.2.16'
    
    testCompile 'junit:junit:4.12'
    testCompile 'edu.wpi.first.wpilibj:wpilibj-java:' + getWpilibjVersion()
    testRuntime 'edu.wpi.first.ntcore:ntcore-jni:' + getNtCoreVersion() + ':all'
    testRuntime 'edu.wpi.first.cscore:cscore-jni:' + getCsCoreVersion() + ':all'
    testRuntime 'org.opencv:opencv-java:' + getWpilibOpencvVersion()
    testRuntime 'org.opencv:opencv-jni:' + getWpilibOpencvVersion() + ':all'
    testRuntime combineNativeLibrariesIntoOne.outputs.files
    
    testCompile project(':ctre_override')
    testCompile "openrio.mirror.third.ctre:CTRE-phoenix-java:" + getCtreVersion()
    testCompile "openrio.mirror.third.kauailabs:navx-java:" + getNavxVersion()
}

def jniClasses = [
    'com.snobot.simulator.jni.DriverStationSimulatorJni',
    'com.snobot.simulator.jni.RegisterCallbacksJni',
    'com.snobot.simulator.jni.adx_family.SpiI2CSimulatorJni',
    'com.snobot.simulator.jni.can.CanCallbackJni',
    'com.snobot.simulator.jni.navx.NavxSimulatorJni',
    'com.snobot.simulator.jni.standard_components.AnalogCallbackJni',
    'com.snobot.simulator.jni.standard_components.AnalogGyroCallbackJni',
    'com.snobot.simulator.jni.standard_components.DigitalCallbackJni',
    'com.snobot.simulator.jni.standard_components.EncoderCallbackJni',
    'com.snobot.simulator.jni.standard_components.I2CCallbackJni',
    'com.snobot.simulator.jni.standard_components.PcmCallbackJni',
    'com.snobot.simulator.jni.standard_components.PdpCallbackJni',
    'com.snobot.simulator.jni.standard_components.PwmCallbackJni',
    'com.snobot.simulator.jni.standard_components.RelayCallbackJni',
    'com.snobot.simulator.jni.standard_components.SpiCallbackJni',
]
    
model {
    jniConfigs {
        snobotSimJavaJniShared(JNIConfig) {
            jniDefinitionClasses = jniClasses
            jniArmHeaderLocations = [ all: file("${projectDir}/src/arm-linux-jni") ]
            sourceSets = [ project.sourceSets.main ]
        }
        snobotSimJavaJniStatic(JNIConfig) {
            jniDefinitionClasses = jniClasses
            jniArmHeaderLocations = [ all: file("${projectDir}/src/arm-linux-jni") ]
            sourceSets = [ project.sourceSets.main ]
        }
    }
    exportsConfigs {
        snobotSimJavaJniShared(ExportsConfig) {
            x86SymbolFilter = { symbols->
                def retList = []
                return retList
            }
            x64SymbolFilter = { symbols->
                def retList = []
                return retList
            }
        }
        snobotSimJavaJniStatic(ExportsConfig) {
            x86SymbolFilter = { symbols->
                def retList = []
                return retList
            }
            x64SymbolFilter = { symbols->
                def retList = []
                return retList
            }
        }
    }
    dependencyConfigs {
        wpiutil(DependencyConfig) {
            groupId = 'edu.wpi.first.wpiutil'
            artifactId = 'wpiutil-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = '+'
            sharedConfigs = [ snobotSimJavaJniShared: [] ]
            staticConfigs = [ snobotSimJavaJniStatic: [] ]
        }
        halsim(DependencyConfig) {
            groupId = 'edu.wpi.first.hal'
            artifactId = 'hal'
            headerClassifier = 'headers'
            ext = 'zip'
            version = getHalHeadersVersion()
            sharedConfigs = [ snobotSimJavaJniShared: [] ]
        }
        adxfamily(DependencyConfig) {
            groupId = 'edu.wpi.first.halsim'
            artifactId = 'halsim-adx_gyro_accelerometer'
            headerClassifier = 'headers'
            ext = 'zip'
            version = getHalHeadersVersion()
            sharedConfigs = [ snobotSimJavaJniShared: [] ]
        }
    }
    components {
        snobotSimJavaJniShared(NativeLibrarySpec) {
            baseName = 'snobotSimJavaJni'
            sources {
                cpp {
                    source {
                        srcDirs = ['cpp/lib']
                        includes = ['**/*.cpp']
                    }
                    exportedHeaders {
                        srcDirs = ["cpp/lib"]
                    }
                    lib project: ':sim_extension_navx', library: 'navx_simulator', linkage: 'shared'
                }
            }
        }

    }
    binaries {
        withType(NativeBinarySpec) {
            it.lib project: ':ctre_override', library: 'CtreMockHooks', linkage: 'shared'
        }
        withType(StaticLibraryBinarySpec) {
            it.buildable = false
        }
    }
}

tasks.withType(Test) {
    systemProperty "java.library.path", "$rootDir/ctre_override/build/libs/ctrePhoenixJava/shared/x86-64"
}

/////////////////////////////////////////////////////////////////////////////////
// Style/Formatting stuff
checkstyle {
    toolVersion = "8.1"
    configFile = new File(rootDir, "styleguide/checkstyle.xml")
    checkstyleTest.enabled = false
}

pmd {
    consoleOutput  = true
    reportsDir = file("${project.buildDir}/reports/pmd")
    ruleSetFiles = files(file("$rootDir/styleguide/pmd-ruleset.xml"))
    ruleSets = []
    pmdTest.enabled = false
}

findbugs {
    excludeFilter = file("$rootDir/styleguide/findbugs_suppressions.xml")
    effort = "max"
    findbugsTest.enabled = false
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
/////////////////////////////////////////////////////////////////////////////////

apply from: "publish.gradle"
